= Personal .vim
:author: Maxim Kim
:experimental:
:autofit-option:
:sectnumlevels: 4
:source-highlighter: rouge
:rouge-style: github
:imagesdir: images


Nothing fancy, just to keep it safe and available.

== Colors
Usually I prefer light background colors.

.https://github.com/habamax/vim-colors-defminus[vim-defminus]
image::img_README_1.png[]

But sometimes there is a room for a dark background.

.https://github.com/habamax/vim-colors-defnoche[vim-defnoche]
image::img_README_2.png[]


.Toggling between both with `yot`
[source,vim]
--------

let g:duo_themes = ['defminus', 'defnoche']
fun! ToggleColorscheme()
	let color = filter(copy(g:duo_themes), {k,v-> v != g:colors_name})[0]
	exe "colorscheme ".color
endf

exe "colorscheme ".g:duo_themes[0]

" Mimic toggling behaviour of Tim Popes unimpaired plugin
nnoremap yot :call ToggleColorscheme()<CR>

--------


== Folding text

Don't really like default folding text vim has. But it is quite easy to change.

[source,vim]
--------

" fill chars to be spaces instead of dashes
set fillchars=fold:\ ,vert:│

" My fancy foldtext
set foldtext=MyFoldText()
fu! MyFoldText()
	let line = getline(v:foldstart)

	" markdown frontmatter -- just take the next line hoping it would be
	" title: Your title
	if line =~ '^----*$'
		let line = getline(v:foldstart+1)
	endif

	let indent = max([indent(v:foldstart)-v:foldlevel, 1])
	let lines = (v:foldend - v:foldstart + 1)
	let strip_line = substitute(line, '^//\|=\+\|["#]\|/\*\|\*/\|{{{\d\=\|title:\s*', '', 'g')
	let strip_line = substitute(strip_line, '^[[:space:]]*\|[[:space:]]*$', '', 'g')
	let text = strpart(strip_line, 0, winwidth(0) - v:foldlevel - indent - 6 - strlen(lines))
	if strlen(strip_line) > strlen(text)
		let text = text.'…'
	endif
	return repeat('•', v:foldlevel) . repeat(' ', indent) . text .' ('. lines .')'
endfu

--------

